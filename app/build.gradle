plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.gms.google-services'
}

// Creates a variable called keystorePropertiesFile, and initializes it to the
// keystore.properties file.

// !! You need to create your own local file to build release version !!
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initializes a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Loads the keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    namespace 'com.romeat.smashup'
    compileSdk 33

    defaultConfig {
        applicationId "com.romeat.smashup"
        minSdk 24
        targetSdk 33
        versionCode 20
        versionName "0.6.2"
        setProperty("archivesBaseName", "smashup-v" + versionName)

        resourceConfigurations += ["en", "ru"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            buildConfigField "String", 'API_URL', '"https://api.smashup.ru"'
            resValue "string", "app_name", "@string/app_name_debug"
            versionNameSuffix "-D"
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", 'API_URL', '"https://api.smashup.ru"'
            resValue "string", "app_name", "@string/app_name_release"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    // changing material version may fuck up bitrate slider, be careful
    implementation 'androidx.compose.material:material:1.3.1'
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "androidx.compose.ui:ui-util:$compose_ui_version"

    implementation(platform("com.google.firebase:firebase-bom:32.1.1"))
    implementation 'com.google.firebase:firebase-messaging-ktx:23.0.2'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'

    implementation 'androidx.work:work-runtime:2.7.1'

    // Dagger-Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-android-compiler:2.44"
    //implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Retrofit
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Glide
    api 'com.github.bumptech.glide:glide:4.15.0'
    implementation "com.github.bumptech.glide:compose:1.0.0-alpha.1"

    // Glide with shimmer effect
    def landscapist_ver = '2.2.2'
    //implementation "com.github.skydoves:landscapist-glide:1.3.6"
    implementation "com.github.skydoves:landscapist-glide:$landscapist_ver"
    implementation "com.github.skydoves:landscapist-placeholder:$landscapist_ver"

    // Some converter for http response
    implementation 'com.squareup.retrofit2:converter-scalars:2.5.0'

    // OKHttp
    implementation("com.squareup.okhttp3:okhttp:4.10.0")
    implementation("com.squareup.okhttp3:okhttp-urlconnection:4.2.2")


    implementation("androidx.navigation:navigation-compose:2.5.3")

    // Exoplayer
    implementation("androidx.media:media:1.6.0")
    def exoplayer_version = '2.18.6'
    api "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    api "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"
    api "com.google.android.exoplayer:extension-mediasession:$exoplayer_version"
    api "com.google.android.exoplayer:extension-cast:$exoplayer_version"

    // Accompanist for navigation animations
    implementation "com.google.accompanist:accompanist-navigation-animation:0.28.0"

    // preferences DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"
}
